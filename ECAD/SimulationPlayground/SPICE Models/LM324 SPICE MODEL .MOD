* BEGIN SPICE MODEL LM324
* ON SEMICONDUCTOR NEXT GEN MODEL 9/27/2018
* MODEL FEATURES INCLUDE OUTPUT SWING, OUTPUT CURRENT
* THROUGH THE SUPPLY RAILS, CLASS B OUTPUT STAGE, OUTPUT
* SWING, OPEN LOOP GAIN AND PHASE WITH RL AND CL EFFECTS,
* POWER SUPPLY REJECTION WITH FREQUENCY EFFECTS, COMMON
* MODE REJECTION WITH FREQUENCY EFFECTS, INPUT VOLTAGE
* NOISE WITH 1/F, INPUT CURRENT NOISE, INPUT BIAS AND
* OFFSET CURRENT, INPUT COMMON MODE VOLTAGE RANGE, VOS
* WITH TEMPERATURE EFFECTS, AND QUIESCENT CURRENT WITH
* TEMPERATURE EFFECTS.
* MODEL TOTAL SUPPLY VOLTAGE RANGE IS 3 TO 36 V.
* MODEL TEMP RANGE IS -55 TO +125 DEG C.
* NOTE THAT MODEL IS FUNCTIONAL OVER THIS TEMP RANGE
* BUT NOT ALL PARAMETERS TRACK THOSE OF THE REAL PART.
* NOTE THAT LIKE THE REAL PART THERE IS NO AB BIAS
* IN THE OUTPUT STAGE WHICH UNDER SOME CIRCUMSTANCES
* CAUSES HIGH OUTPUT IMPEDANCE AND OR CROSSOVER DELAY
* WHICH CAN EFFECT THE PERFORMANCE IN SOME CIRCUITS.
* THE COMBINATION OF OUTPUT CURRENT SINK AND NO AB
* BIAS WHEN USED WITH A LARGE CLOAD CAN CAUSE SOME
* INTERESTING SMALL SIGNAL STEP OVERSHOOTS THAT ARE
* LARGER THAN NORMAL AND RAMP LINEARLY RATHER THAN
* BEING SHAPED LIKE A CLASSIC OVERSHOOT. THIS HAPPENS
* IDENTICALLY WITH THE REAL PART AND THE MODEL.
*
* PINOUT ORDER +IN -IN +V -V OUT
* PINOUT ORDER  1   2   3  4  5
.SUBCKT LM324 1 2 3 4 5
R44 4 6 4E4
I1 4 7 0.5E-6
Q1 4 8 9 QPI
Q2 4 2 10 QPA
Q3 9 9 11 QPI
Q4 10 10 11 QPI
Q5 12 13 4 QNQ
Q6 13 13 4 QNQ
Q7 4 12 14 QPQ
Q8 3 14 6 QNQ
Q9 15 6 4 QNQ
Q10 3 15 16 QNQ
Q11 3 16 17 QNQ
R67 17 16 4E4
R68 5 17 18
Q12 4 15 5 QPQ
Q13 15 17 5 QNQ
I2 18 3 120E-9
I3 19 3 60E-9
I4 20 3 1E-6
Q14 11 18 3 QPQ
Q15 14 19 3 QPQ
Q16 5 7 4 QNQ
Q17 15 20 3 QPQ
C15 21 22 4.8E-12
R69 12 21 3
R70 12 15 3E9
E2 23 8 3 0 -10E-6
V51 23 1 1.56E-3
I6 3 4 5E-6
R71 4 3 4.5E5
Q18 12 9 11 QPQ
Q19 13 10 11 QPQ
C17 12 13 8E-12
C18 6 15 1E-12
C21 3 24 100E-15
R78 11 24 3E5
C22 1 2 0.23E-12
C23 2 0 0.79E-12
C24 1 0 0.79E-12
E3 22 0 15 0 2
C25 5 0 50E-15
Q20 25 25 0 QNQ
G1 3 4 VT 0 3E-4
I7 0 25 1E-3
V53 25 26 0.25
R79 0 26 1E6
E4 VT 0 27 26 1
R80 0 VT 1E6
V54 27 0 0.55
R81 0 27 1E6
.MODEL QPQ PNP IKF=3E-3 RC=300 KF=4.8E-17 BR=1
.MODEL QPA PNP IKF=3E-3 RC=380 IS=1.01E-16 VAF=245 RE=5 RB=1700 BF=300 KF=4.8E-17 BR=1
.MODEL QPI PNP IKF=3E-3 RC=380 IS=1.01E-16 VAF=290 RE=5 RB=1700 BF=306 KF=4.8E-17 BR=1
.MODEL QNQ NPN IKF=5E-3 RC=25  KF=4.8E-17 BR=1
.ENDS
* END SPICE MODEL LM324
